#!/usr/bin/env perl
use strict;
use warnings;
use YAML::Syck 'LoadFile';

@ARGV = glob("log/requests/*.log") if @ARGV == 0;
die "No files to analyze" if @ARGV == 0;

my @hits;

for my $logfile (@ARGV) {
    my @entries = LoadFile($logfile);
    for (my $i = 1; $i < @entries; ++$i) {
        my $delta_memory = $entries[$i]{memory} - $entries[$i-1]{memory};
        my $path = $entries[$i]{ENV}{PATH_INFO};
        push @hits, [$delta_memory, $path] if $delta_memory != 0;
    }
}

for my $hit (sort { $b->[0] <=> $a->[0] } @hits) {
    my $human_readable = sub {
        my $bytes = shift;
        return $bytes if $bytes < 1024;
        return int($bytes / 1024)    . 'KB' if $bytes < 1024 ** 2;
        return int($bytes / 1024**2) . 'MB' if $bytes < 1024 ** 3;
        return int($bytes / 1024**3) . 'GB';
    }->($hit->[0]);

    $human_readable =~ s/^(?!-)/+/; # add leading + if it's not negative

    print "$hit->[1]: $human_readable\n";
}

